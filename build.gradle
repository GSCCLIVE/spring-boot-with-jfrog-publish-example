plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'maven-publish'
	id 'com.jfrog.artifactory' version '4.17.2'
}

group = 'sample.project.java'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	maven {
		url "${artifactory_contextUrl}/maven-group"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'io.projectreactor:reactor-test'
}

test {
	useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact bootJar
        }
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = version.endsWith('SNAPSHOT') || version.endsWith('S') ? 'gradle-dev-local': 'gradle-release-local'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }	
		defaults {
			publications('mavenJava')
			publishBuildInfo = true
			publishArtifacts = true
			publishPom = true
		}
    }
    resolve {
        repository {
            repoKey = 'maven-group'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}
